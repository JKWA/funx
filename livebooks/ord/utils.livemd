# Funx.Ord.Utils

```elixir
Mix.install([
  {:funx,
    git: "https://github.com/JKWA/funx.git",
    branch: "main"
  }
])
```

Utility functions for working with the `Funx.Ord` protocol.
These functions assume that types passed in either support Elixir's comparison operators
or implement the `Funx.Ord` protocol.

## Function Examples

```elixir
import Funx.Ord.Utils
```

## contramap/2

Transforms an ordering by applying a function `f` to values before comparison.

The `ord` parameter can be an `Ord` module or a custom comparator map with comparison functions (`:lt?`, `:le?`, `:gt?`, and `:ge?`).
When an `Ord` module is provided, it wraps the module's functions to apply `f` to each value before invoking the comparison.
If a custom comparator map is provided, it wraps the functions in the map to apply `f` to each value.

### Examples

```elixir
ord = Funx.Ord.Utils.contramap(&String.length/1, Funx.Ord.Any)
ord.lt?.("cat", "zebra")
# true
```

## max/3

Returns the maximum of two values, with an optional custom `Ord`.

### Examples

```elixir
Funx.Ord.Utils.max(3, 5)
# 5

ord = Funx.Ord.Utils.contramap(&String.length/1, Funx.Ord.Any)
Funx.Ord.Utils.max("cat", "zebra", ord)
# "zebra"
```

## min/3

Returns the minimum of two values, with an optional custom `Ord`.

### Examples

```elixir
Funx.Ord.Utils.min(10, 7)
# 7

ord = Funx.Ord.Utils.contramap(&String.length/1, Funx.Ord.Any)
Funx.Ord.Utils.min("apple", "kiwi", ord)
# "kiwi"
```

## clamp/4

Clamps a value between `min` and `max`, with an optional custom `Ord`.

### Examples

```elixir
Funx.Ord.Utils.clamp(5, 1, 10)
# 5

Funx.Ord.Utils.clamp(0, 1, 10)
# 1

Funx.Ord.Utils.clamp(15, 1, 10)
# 10
```

## between/4

Checks if `value` is between `min` and `max`, inclusive, with an optional custom `Ord`.

### Examples

```elixir
Funx.Ord.Utils.between(5, 1, 10)
# true

Funx.Ord.Utils.between(0, 1, 10)
# false

Funx.Ord.Utils.between(11, 1, 10)
# false
```

## compare/3

Compares two values and returns `:lt`, `:eq`, or `:gt`, with an optional custom `Ord`.

### Examples

```elixir
Funx.Ord.Utils.compare(3, 5)
# :lt

Funx.Ord.Utils.compare(7, 7)
# :eq

Funx.Ord.Utils.compare(9, 4)
# :gt
```

## reverse/1

Reverses the ordering logic.

### Examples

```elixir
ord = Funx.Ord.Utils.reverse(Funx.Ord.Any)
ord.lt?.(10, 5)
# true
```

## comparator/1

Creates a comparator function from the given `Ord` module, returning `true`
if `a` is less than or equal to `b` according to the module's ordering.

Useful for sorting with `Enum.sort/2` or similar functions.

### Examples

```elixir
comparator = Funx.Ord.Utils.comparator(Funx.Ord.Any)
Enum.sort([3, 1, 2], comparator)
# [1, 2, 3]
```

## to_eq/1

Converts an `Ord` instance into an equality comparator.

This function creates a map containing two functions:
  - `eq?/2`: Returns `true` if `a` and `b` are considered equal by the given `Ord`.
  - `not_eq?/2`: Returns `true` if `a` and `b` are not considered equal by the given `Ord`.

### Examples

```elixir
eq = Funx.Ord.Utils.to_eq(Funx.Ord.Any)
eq.eq?.(5, 5)
# true
```

## append/2

Appends two `Ord` instances, combining their comparison logic.

If the first `Ord` comparator determines an order, that result is used.
If not, the second comparator is used as a fallback.

### Examples

```elixir
ord1 = Funx.Ord.Utils.contramap(& &1.age, Funx.Ord.Any)
ord2 = Funx.Ord.Utils.contramap(& &1.name, Funx.Ord.Any)
combined = Funx.Ord.Utils.append(ord1, ord2)
combined.lt?.(%{age: 30, name: "Alice"}, %{age: 30, name: "Bob"})
# true
```

## concat/1

Concatenates a list of `Ord` instances into a single composite comparator.

This function reduces a list of `Ord` comparators into a single `Ord`,
applying them in sequence until an order is determined.

### Examples

```elixir
ord_list = [
  Funx.Ord.Utils.contramap(& &1.age, Funx.Ord.Any),
  Funx.Ord.Utils.contramap(& &1.name, Funx.Ord.Any)
]
combined = Funx.Ord.Utils.concat(ord_list)
combined.gt?.(%{age: 25, name: "Charlie"}, %{age: 25, name: "Bob"})
# true
```